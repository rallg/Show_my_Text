project('Show My Text', 'c',
    version: '41.0.1',
    license: 'GPLv2+',
    meson_version: '>= 0.50.0',
    default_options: [
        'prefix=/usr/local',
        'bindir=/usr/local/bin',
        'datadir=/usr/local/share/applications'
    ]
)

cc = meson.get_compiler('c')
i18n = import('i18n')

glib_req_version = '>= 2.56.0'
gtk_req_version = '>= 3.24.1'
libhandy_req_version = '>= 1.0.0'
harfbuzz_req_version = '>= 0.9.9'
glib_dep = dependency('glib-2.0', version: glib_req_version)
gtk_dep = dependency('gtk+-3.0', version: gtk_req_version)
libhandy_dep = dependency('libhandy-1', version: libhandy_req_version)
harfbuzz_dep = dependency('harfbuzz', version: harfbuzz_req_version)
fontconfig_dep = dependency('fontconfig')
freetype2_dep = dependency('freetype2')
mathlib_dep = cc.find_library('m')

config_h = configuration_data()
config_h.set_quoted('VERSION', meson.project_version())
config_h.set_quoted('GETTEXT_PACKAGE', meson.project_name())
config_h.set_quoted('APPLICATION_ID', '')
configure_file(output: 'config.h', configuration: config_h)

root_inc = include_directories('.')
subdir('src')

meson.add_install_script('src/postinstall-linux.sh')
